/*
 * Make sure that you use the latest version of the module by changing the
 * `ref=` value in the `source` attribute to the latest version listed on the
 * releases page of this repository.
 *
 */

 /* Bucket used to store template files and other resources needed to create Excel spreadsheets */
module "s3_bucket" {
  source = "github.com/ministryofjustice/cloud-platform-terraform-s3-bucket?ref=5.3.0"

  team_name              = var.team_name
  business_unit          = var.business_unit
  application            = var.application
  is_production          = var.is_production
  environment_name       = var.environment
  infrastructure_support = var.infrastructure_support
  namespace              = var.namespace

  # Buckets are private by default

  bucket_name = var.file_store_bucket_name
  versioning = true
}

resource "kubernetes_secret" "s3_bucket" {
  metadata {
    name      = "s3-bucket-output"
    namespace = var.namespace
  }

  data = {
    bucket_arn  = module.s3_bucket.bucket_arn
    bucket_name = module.s3_bucket.bucket_name
  }
}

/* Bucket used to store files generated by the Bulk Claims Report Service that GPFD/GLAD will serve to users */
module "s3_bucket_report_store" {
  source = "github.com/ministryofjustice/cloud-platform-terraform-s3-bucket?ref=5.3.0"

  team_name              = var.team_name
  business_unit          = var.business_unit
  application            = var.application
  is_production          = var.is_production
  environment_name       = var.environment
  infrastructure_support = var.infrastructure_support
  namespace              = var.namespace

  bucket_name = var.report_store_bucket_name
  versioning = false

  lifecycle_rule = [
    {
      enabled = true
      id      = "Delete any report if last updated more than 30 days ago"
      prefix  = "reports/"
      expiration = [
        {
          days = 30
        }
      ]
    }
  ]

  logging_enabled        = true
  log_target_bucket      = module.s3_bucket_report_store_logging.bucket_name
  log_path               = "logs/"

}

resource "kubernetes_secret" "s3_bucket_report_store" {
  metadata {
    name      = "s3-bucket-report-store-output"
    namespace = var.namespace
  }

  data = {
    bucket_arn  = module.s3_bucket_report_store.bucket_arn
    bucket_name = module.s3_bucket_report_store.bucket_name
  }
}

/* Bucket used to store logs on what reports are being accessed / downloaded */
module "s3_bucket_report_store_logging" {
  source = "github.com/ministryofjustice/cloud-platform-terraform-s3-bucket?ref=5.3.0"

  team_name              = var.team_name
  business_unit          = var.business_unit
  application            = var.application
  is_production          = var.is_production
  environment_name       = var.environment
  infrastructure_support = var.infrastructure_support
  namespace              = var.namespace

  bucket_name = var.s3_bucket_report_store_logging

  bucket_policy = jsonencode({
      Version = "2012-10-17",
      Statement = [
        {
          Sid    = "S3ServerAccessLogsPolicy",
          Effect = "Allow",
          Principal = {
            Service = "logging.s3.amazonaws.com"
          },
          Action = [
            "s3:PutObject"
          ],
          Resource = "$${bucket_arn}/*"
        },
        {
          Effect = "Allow"
          Principal = {
            AWS = [
              module.irsa.role_arn
            ]
          }
          Action = [
            "s3:GetObject"
          ]
          Resource = [
            "$${bucket_arn}",
            "$${bucket_arn}/*"
          ]
        }
      ]
    })

}

resource "kubernetes_secret" "s3_bucket_report_store_logging" {
  metadata {
    name      = "s3-bucket-report-store-logging-output"
    namespace = var.namespace
  }

  data = {
    bucket_arn  = module.s3_bucket_report_store_logging.bucket_arn
    bucket_name = module.s3_bucket_report_store_logging.bucket_name
  }
}
