module "event_pnd_queue" {
  source = "github.com/ministryofjustice/cloud-platform-terraform-sqs?ref=5.1.2"

  # Queue configuration
  sqs_name                  = "events_pnd_queue"
  encrypt_sqs_kms           = "true"
  message_retention_seconds = 1209600

  redrive_policy = <<EOF
  {
    "deadLetterTargetArn": "${module.event_pnd_dead_letter_queue.sqs_arn}","maxReceiveCount": 3
  }
  EOF

  # Tags
  business_unit          = var.business_unit
  application            = var.application
  is_production          = var.is_production
  team_name              = var.team_name
  namespace              = var.namespace
  environment_name       = var.environment
  infrastructure_support = var.infrastructure_support
}

module "event_pnd_dead_letter_queue" {
  source = "github.com/ministryofjustice/cloud-platform-terraform-sqs?ref=5.1.2"

  # Queue configuration
  sqs_name        = "event_pnd_queue_dl"
  encrypt_sqs_kms = "true"

  # Tags
  business_unit          = var.business_unit
  application            = var.application
  is_production          = var.is_production
  team_name              = var.team_name
  namespace              = var.namespace
  environment_name       = var.environment
  infrastructure_support = var.infrastructure_support
}

resource "aws_sqs_queue_policy" "event_pnd_queue_policy" {
  queue_url = module.event_pnd_queue.sqs_id

  policy = <<EOF
  {
    "Version": "2012-10-17",
    "Id": "${module.event_pnd_queue.sqs_arn}/SQSDefaultPolicy",
    "Statement":
      [
        {
          "Effect": "Allow",
          "Principal": {"AWS": "*"},
          "Resource": "${module.event_pnd_queue.sqs_arn}",
          "Action": "SQS:SendMessage",
          "Condition":
            {
              "ArnEquals":
                {
                  "aws:SourceArn": "${module.hmpps-integration-events.topic_arn}"
                }
            }
        }
      ]
  }
   EOF
  depends_on = [
    module.hmpps-integration-events
  ]
}


data "aws_secretsmanager_secret" "pnd_filter_list" {
  # NB secret name is generated by secret module and could be change. Current version 3.0.0 does not output the name of the secret.
  name = "live-hmpps-integration-api-prod-44b3dec538976ac2"
}

data "aws_secretsmanager_secret_version" "pnd_filter_list" {
  secret_id = data.aws_secretsmanager_secret.pnd_filter_list.id
}


resource "aws_sns_topic_subscription" "event_pnd_subscription" {
  topic_arn     = module.hmpps-integration-events.topic_arn
  protocol      = "sqs"
  endpoint      = module.event_pnd_queue.sqs_arn
  filter_policy = data.aws_secretsmanager_secret_version.pnd_filter_list.secret_string
  depends_on = [
    module.hmpps-integration-events
  ]
}

resource "kubernetes_secret" "event_pnd_queue" {
  metadata {
    name      = "event-pnd-queue"
    namespace = var.namespace
  }

  data = {
    sqs_id                      = module.event_pnd_queue.sqs_id
    sqs_arn                     = module.event_pnd_queue.sqs_arn
    sqs_name                    = module.event_pnd_queue.sqs_name
    pnd_filter_policy_secret_id = data.aws_secretsmanager_secret_version.pnd_filter_list.secret_id
  }
}

resource "kubernetes_secret" "event_pnd_dead_letter_queue" {
  metadata {
    name      = "event-pnd-dl-queue"
    namespace = var.namespace
  }

  data = {
    sqs_id   = module.event_pnd_dead_letter_queue.sqs_id
    sqs_arn  = module.event_pnd_dead_letter_queue.sqs_arn
    sqs_name = module.event_pnd_dead_letter_queue.sqs_name
  }
}
